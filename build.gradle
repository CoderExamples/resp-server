apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.bmuschko.nexus'

group = 'com.github.tonivade'
archivesBaseName = 'resp-server'
version = '0.6.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    compile 'io.netty:netty-all:4.1.9.Final'
    compile 'com.github.tonivade:equalizer:0.2.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.javaslang:javaslang:2.0.5'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.7.18'
}

modifyPom {
    project {
        name 'RESP Server'
        description 'Netty implementation of REdis Serialization Protocol, and a simple framework to implement command based protocols'
        url 'https://github.com/tonivade/resp-server'
        inceptionYear '2015'

        scm {
            url 'https://github.com/tonivade/resp-server'
            connection 'scm:https://github.com/tonivade/resp-server.git'
            developerConnection 'scm:git://github.com/tonivade/resp-server.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://github.com/tonivade/resp-server/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'tonivade'
                name 'Antonio Mu√±oz'
                email 'antoniogmc@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}

