apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.bmuschko.nexus'

group = 'com.github.tonivade'
archivesBaseName = 'resp-server'
version = '0.8.0-SNAPSHOT'

configurations {
  codacy
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url = "http://dl.bintray.com/typesafe/maven-releases" 
    }
    maven { 
      url = "https://jitpack.io"
    }
}

dependencies {
    compile 'io.netty:netty-all:4.1.14.Final'
    compile 'com.github.tonivade:equalizer:0.2.0'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.vavr:vavr:0.9.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    runtime 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.8.47'
    codacy("com.github.codacy:codacy-coverage-reporter:-SNAPSHOT")
}

modifyPom {
    project {
        name 'RESP Server'
        description 'Netty implementation of REdis Serialization Protocol, and a simple framework to implement command based protocols'
        url 'https://github.com/tonivade/resp-server'
        inceptionYear '2015'

        scm {
            url 'https://github.com/tonivade/resp-server'
            connection 'scm:https://github.com/tonivade/resp-server.git'
            developerConnection 'scm:git://github.com/tonivade/resp-server.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://github.com/tonivade/resp-server/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'tonivade'
                name 'Antonio Mu√±oz'
                email 'antoniogmc@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
  main = "com.codacy.CodacyCoverageReporter"
  classpath = configurations.codacy
  args = ["-l", "Java", "-r", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"]
}
