apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.bmuschko.nexus'

group = 'com.github.tonivade'
archivesBaseName = 'resp-server'
version = '0.5.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    compile 'io.netty:netty-all:4.0.42.Final'
    compile 'com.github.tonivade:equalizer:0.2.0'
    compile 'io.reactivex:rxjava:1.2.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.2.6'
}

modifyPom {
    project {
        name 'Tiny Server'
        description 'Command based TCP server framework'
        url 'https://github.com/tonivade/tiny-server'
        inceptionYear '2015'

        scm {
            url 'https://github.com/tonivade/tiny-server'
            connection 'scm:https://github.com/tonivade/tiny-server.git'
            developerConnection 'scm:git://github.com/tonivade/tiny-server.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://github.com/tonivade/tiny-server/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'tonivade'
                name 'Antonio Mu√±oz'
                email 'antoniogmc@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}
